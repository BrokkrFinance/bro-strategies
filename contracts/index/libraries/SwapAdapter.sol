// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.0;

import { Errors } from "./Errors.sol";

import { ICamelotPair } from "../dependencies/ICamelotPair.sol";
import { ICamelotRouter } from "../dependencies/ICamelotRouter.sol";
import { IChronosFactory } from "../dependencies/IChronosFactory.sol";
import { IChronosPair } from "../dependencies/IChronosPair.sol";
import { IChronosRouter } from "../dependencies/IChronosRouter.sol";
import { ITraderJoeV2Pair } from "../dependencies/ITraderJoeV2Pair.sol";
import { ITraderJoeV2Point1Pair } from "../dependencies/ITraderJoeV2Point1Pair.sol";
import { ITraderJoeV2Router } from "../dependencies/ITraderJoeV2Router.sol";
import { ITraderJoeV2Point1Router } from "../dependencies/ITraderJoeV2Point1Router.sol";
import { IUniswapV2Pair } from "../dependencies/IUniswapV2Pair.sol";
import { IUniswapV2Router } from "../dependencies/IUniswapV2Router.sol";
import { CamelotLibrary } from "./CamelotLibrary.sol";
import { ChronosLibrary } from "./ChronosLibrary.sol";
import { TraderJoeV2Library } from "./TraderJoeV2Library.sol";
import { TraderJoeV2Point1Library } from "./TraderJoeV2Point1Library.sol";
import { UniswapV2Library } from "./UniswapV2Library.sol";
import { IQuoterV2 } from "@uniswap/v3-periphery/contracts/interfaces/IQuoterV2.sol";
import { ISwapRouter } from "@uniswap/v3-periphery/contracts/interfaces/ISwapRouter.sol";
import { IUniswapV3Pool } from "@uniswap/v3-core/contracts/interfaces/IUniswapV3Pool.sol";
import { IERC20Upgradeable } from "@openzeppelin/contracts-upgradeable/interfaces/IERC20Upgradeable.sol";

import "hardhat/console.sol";

struct UniswapV3PairData {
    uint24[] fees;
    IQuoterV2 quoter;
}

library SwapAdapter {
    using CamelotLibrary for ICamelotRouter;
    using ChronosLibrary for IChronosRouter;
    using UniswapV2Library for IUniswapV2Router;
    using TraderJoeV2Library for ITraderJoeV2Router;
    using TraderJoeV2Point1Library for ITraderJoeV2Point1Router;

    enum DEX {
        None,
        UniswapV2,
        TraderJoeV2,
        Camelot,
        Chronos,
        TraderJoeV2_1,
        UniswapV3
    }

    struct PairData {
        address pair;
        bytes data; // Pair specific data such as bin step of TraderJoeV2, pool fee of Uniswap V3, etc.
    }

    struct Setup {
        DEX dex;
        address router;
        PairData pairData;
    }

    function swapExactTokensForTokens(
        Setup memory setup,
        uint256 amountIn,
        uint256 amountOutMin,
        address[] memory path
    ) external returns (uint256 amountOut) {
        if (path[0] == path[path.length - 1]) {
            return amountIn;
        }

        if (setup.dex == DEX.UniswapV2) {
            return
                IUniswapV2Router(setup.router).swapExactTokensForTokens(
                    amountIn,
                    amountOutMin,
                    path
                );
        }

        if (setup.dex == DEX.TraderJoeV2) {
            uint256[] memory binSteps = new uint256[](1);
            binSteps[0] = abi.decode(setup.pairData.data, (uint256));

            return
                ITraderJoeV2Router(setup.router).swapExactTokensForTokens(
                    amountIn,
                    amountOutMin,
                    binSteps,
                    path
                );
        }

        if (setup.dex == DEX.Camelot) {
            return
                ICamelotRouter(setup.router).swapExactTokensForTokens(
                    amountIn,
                    amountOutMin,
                    path
                );
        }

        if (setup.dex == DEX.Chronos) {
            return
                IChronosRouter(setup.router).swapExactTokensForTokens(
                    amountIn,
                    amountOutMin,
                    path
                );
        }

        if (setup.dex == DEX.TraderJoeV2_1) {
            uint256[] memory binSteps = new uint256[](1);
            binSteps[0] = abi.decode(setup.pairData.data, (uint256));

            return
                ITraderJoeV2Point1Router(setup.router).swapExactTokensForTokens(
                    amountIn,
                    amountOutMin,
                    binSteps,
                    path
                );
        }

        if (setup.dex == DEX.UniswapV3) {
            if (path.length != 2) {
                revert Errors.SwapAdapter_WrongPathLength(path.length);
            }

            UniswapV3PairData memory uniswapV3PairData = abi.decode(
                setup.pairData.data,
                (UniswapV3PairData)
            );

            IERC20Upgradeable(path[0]).approve(address(setup.router), amountIn);

            return
                ISwapRouter(setup.router).exactInputSingle(
                    ISwapRouter.ExactInputSingleParams(
                        path[0],
                        path[1],
                        uniswapV3PairData.fees[0],
                        address(this),
                        block.timestamp,
                        amountIn,
                        amountOutMin,
                        0
                    )
                );
        }

        revert Errors.SwapAdapter_WrongDEX(uint8(setup.dex));
    }

    function swapTokensForExactTokens(
        Setup memory setup,
        uint256 amountOut,
        uint256 amountInMax,
        address[] memory path
    ) external returns (uint256 amountIn) {
        if (path[0] == path[path.length - 1]) {
            return amountOut;
        }

        if (setup.dex == DEX.UniswapV2) {
            return
                IUniswapV2Router(setup.router).swapTokensForExactTokens(
                    amountOut,
                    amountInMax,
                    path
                );
        }

        if (setup.dex == DEX.TraderJoeV2) {
            uint256[] memory binSteps = new uint256[](1);
            binSteps[0] = abi.decode(setup.pairData.data, (uint256));

            return
                ITraderJoeV2Router(setup.router).swapTokensForExactTokens(
                    amountOut,
                    amountInMax,
                    binSteps,
                    path
                );
        }

        if (setup.dex == DEX.Camelot) {
            return
                ICamelotRouter(setup.router).swapTokensForExactTokens(
                    amountOut,
                    amountInMax,
                    path
                );
        }

        if (setup.dex == DEX.Chronos) {
            return
                IChronosRouter(setup.router).swapTokensForExactTokens(
                    amountOut,
                    amountInMax,
                    path
                );
        }

        if (setup.dex == DEX.TraderJoeV2_1) {
            uint256[] memory binSteps = new uint256[](1);
            binSteps[0] = abi.decode(setup.pairData.data, (uint256));

            return
                ITraderJoeV2Point1Router(setup.router).swapTokensForExactTokens(
                    amountOut,
                    amountInMax,
                    binSteps,
                    path
                );
        }

        if (setup.dex == DEX.UniswapV3) {
            if (path.length != 2) {
                revert Errors.SwapAdapter_WrongPathLength(path.length);
            }

            UniswapV3PairData memory uniswapV3PairData = abi.decode(
                setup.pairData.data,
                (UniswapV3PairData)
            );

            IERC20Upgradeable(path[0]).approve(
                address(setup.router),
                amountInMax
            );

            return
                ISwapRouter(setup.router).exactOutputSingle(
                    ISwapRouter.ExactOutputSingleParams(
                        path[0],
                        path[1],
                        uniswapV3PairData.fees[0],
                        address(this),
                        block.timestamp,
                        amountOut,
                        amountInMax,
                        0
                    )
                );
        }

        revert Errors.SwapAdapter_WrongDEX(uint8(setup.dex));
    }

    function getAmountOutView(
        Setup memory setup,
        uint256 amountIn,
        address tokenIn,
        address tokenOut
    ) public view returns (uint256 amountOut) {
        if (tokenIn == tokenOut) {
            return amountIn;
        }

        if (setup.dex == DEX.UniswapV2) {
            return
                IUniswapV2Router(setup.router).getAmountOut(
                    IUniswapV2Pair(setup.pairData.pair),
                    amountIn,
                    tokenIn,
                    tokenOut
                );
        } else if (setup.dex == DEX.TraderJoeV2) {
            return
                ITraderJoeV2Router(setup.router).getAmountOut(
                    ITraderJoeV2Pair(setup.pairData.pair),
                    amountIn,
                    tokenIn,
                    tokenOut
                );
        } else if (setup.dex == DEX.Camelot) {
            return
                ICamelotRouter(setup.router).getAmountOut(
                    ICamelotPair(setup.pairData.pair),
                    amountIn,
                    tokenIn
                );
        } else if (setup.dex == DEX.Chronos) {
            return
                IChronosRouter(setup.router).getAmountOut(
                    IChronosPair(setup.pairData.pair),
                    amountIn,
                    tokenIn
                );
        } else if (setup.dex == DEX.TraderJoeV2_1) {
            return
                ITraderJoeV2Point1Router(setup.router).getAmountOut(
                    ITraderJoeV2Point1Pair(setup.pairData.pair),
                    amountIn,
                    tokenIn,
                    tokenOut
                );
        } else if (setup.dex == DEX.UniswapV3) {
            IUniswapV3Pool uniswapV3Pool = IUniswapV3Pool(setup.pairData.pair);
            (uint160 sqrtPriceX96, , , , , , ) = uniswapV3Pool.slot0();

            uint256 priceX96 = ((uint256(sqrtPriceX96) *
                uint256(sqrtPriceX96)) / (2**96));
            address token0 = uniswapV3Pool.token0();

            if (token0 == tokenIn) {
                return (priceX96 * amountIn) / (2**96);
            } else {
                return ((((2**192) / priceX96)) * amountIn) / (2**96);
            }
        }

        revert Errors.SwapAdapter_WrongDEX(uint8(setup.dex));
    }

    function getAmountOut(
        Setup memory setup,
        uint256 amountIn,
        address tokenIn,
        address tokenOut
    ) external returns (uint256 amountOut) {
        if (tokenIn == tokenOut) {
            return amountIn;
        }

        if (setup.dex == DEX.UniswapV3) {
            UniswapV3PairData memory uniswapV3PairData = abi.decode(
                setup.pairData.data,
                (UniswapV3PairData)
            );

            (amountOut, , , ) = IQuoterV2(uniswapV3PairData.quoter)
                .quoteExactInputSingle(
                    IQuoterV2.QuoteExactInputSingleParams(
                        tokenIn,
                        tokenOut,
                        amountIn,
                        uniswapV3PairData.fees[0],
                        0
                    )
                );
            return amountOut;
        } else return getAmountOutView(setup, amountIn, tokenIn, tokenOut);
    }

    function getAmountIn(
        Setup memory setup,
        uint256 amountOut,
        address tokenIn,
        address tokenOut
    ) external returns (uint256 amountIn) {
        if (tokenIn == tokenOut) {
            return amountOut;
        }

        if (setup.dex == DEX.UniswapV2) {
            return
                IUniswapV2Router(setup.router).getAmountIn(
                    IUniswapV2Pair(setup.pairData.pair),
                    amountOut,
                    tokenIn,
                    tokenOut
                );
        }

        if (setup.dex == DEX.TraderJoeV2) {
            return
                ITraderJoeV2Router(setup.router).getAmountIn(
                    ITraderJoeV2Pair(setup.pairData.pair),
                    amountOut,
                    tokenIn,
                    tokenOut
                );
        }

        if (setup.dex == DEX.Camelot) {
            return
                ICamelotRouter(setup.router).getAmountIn(
                    ICamelotPair(setup.pairData.pair),
                    amountOut,
                    tokenOut
                );
        }

        if (setup.dex == DEX.Chronos) {
            address factory = abi.decode(setup.pairData.data, (address));

            return
                IChronosRouter(setup.router).getAmountIn(
                    IChronosPair(setup.pairData.pair),
                    IChronosFactory(factory),
                    amountOut,
                    tokenOut
                );
        }

        if (setup.dex == DEX.TraderJoeV2_1) {
            return
                ITraderJoeV2Point1Router(setup.router).getAmountIn(
                    ITraderJoeV2Point1Pair(setup.pairData.pair),
                    amountOut,
                    tokenIn,
                    tokenOut
                );
        }

        if (setup.dex == DEX.UniswapV3) {
            UniswapV3PairData memory uniswapV3PairData = abi.decode(
                setup.pairData.data,
                (UniswapV3PairData)
            );

            (amountIn, , , ) = IQuoterV2(uniswapV3PairData.quoter)
                .quoteExactOutputSingle(
                    IQuoterV2.QuoteExactOutputSingleParams(
                        tokenIn,
                        tokenOut,
                        amountOut,
                        uniswapV3PairData.fees[0],
                        0
                    )
                );
            return amountIn;
        }

        revert Errors.SwapAdapter_WrongDEX(uint8(setup.dex));
    }
}
